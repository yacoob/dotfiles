set fallback := false

### devpod recipes
#
ensure-host:
  #!/bin/env bash
  if [ -n "$DEVPOD" ]; then
    echo "Run this on the host, not within the container."
    exit 1
  fi

[no-cd]
init-devpod: ensure-host
  devpod up --ide=none .

[no-cd]
raze-devpod: ensure-host
  devpod delete .

[no-cd]
stop-devpod: ensure-host
  devpod stop .

[no-cd]
start-devpod: ensure-host
  devpod up --ide=none .

[no-cd]
cli: ensure-host init-devpod
  devpod ssh .

[private]
[no-cd]
cmd-devpod cmd: ensure-host start-devpod
    #!/bin/env bash
    set -x
    devpod ssh --command 'zsh -i -c "{{ cmd }}"' .

dev-devpod: (cmd-devpod "just dev")

[private]
[no-cd]
nvim-devpod cmd: ensure-host
    #!/bin/env bash
    set -x
    devpod up --ide=none .
    devpod ssh --command 'zsh -i -c "nvim --headless --listen localhost:6000"' . >/dev/null &
    sleep 2
    {{ cmd }}
    devpod ssh --silent --command 'pkill nvim || true' .

nvim: (nvim-devpod "nvim --server localhost:6000 --remote-ui")
neovide: (nvim-devpod "neovide --server localhost:6000")
nvimqt: (nvim-devpod "nvim-qt --server localhost:6000 --nofork")
